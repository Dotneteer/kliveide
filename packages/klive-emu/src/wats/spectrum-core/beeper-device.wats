// ============================================================================
// Beeper device for ZX Spectrum 48/128
// ============================================================================

// ----------------------------------------------------------------------------
// Beeper constants
const u32 GATE_DIVIDER = 100'000;

// ----------------------------------------------------------------------------
// Memory area for beeper samples

u8[0x2000] beeperSamples;

// ----------------------------------------------------------------------------
// Beeper device state

// Sample rate of the beeper audio
global u32 audioSampleRate;

// Sample length (lower) in CPU clock tacts
global u32 audioSampleLength;

// Lower gate for sample length
global u32 audioLowerGate;

// Upper gate for sample length
global u32 audioUpperGate;

// Current beeper gate value
global u32 audioGateValue;

// Tact value of the last sample
global u32 audioNextSampleTact;

// Count of samples in the current frame
global u32 audioSampleCount;

// Last EAR bit value
global u32 beeperLastEarBit;

// ----------------------------------------------------------------------------
// Beeper device routines

// Sets the specified beeper sample rate
// rate: New beeper sample rate
export void setAudioSampleRate(u32 rate) {
  audioSampleRate = rate;
  local f32 sampleLength = (f32(baseClockFrequency) * clockMultiplier) / rate;
  audioSampleLength = sampleLength;
  audioLowerGate = (sampleLength - audioSampleLength) * GATE_DIVIDER;
  audioUpperGate = GATE_DIVIDER;
}

// Renders the next beeper sample
inline void renderBeeperSample() {
  beeperSamples[audioSampleCount] = !!beeperLastEarBit;
}

// Moves to the next audio sample
inline void moveToNextAudioSample() {
  audioSampleCount += 1;
  audioGateValue += audioLowerGate;
  audioNextSampleTact += audioSampleLength;
  if (audioGateValue >= audioUpperGate) {
    audioNextSampleTact += 1;
    audioGateValue -= audioUpperGate;
  }
}
