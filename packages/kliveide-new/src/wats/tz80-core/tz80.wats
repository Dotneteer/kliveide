import void trace "imports" "trace" (u32);

#include "../z80/z80.wats"
#include "../z80/z80-default-handlers.wats"
#include "machine.wats"

// ============================================================================
// Test machine global variables

// Test run mode
global u32 testRunMode;
const u32 RM_NORMAL = 0;
const u32 RM_ONE_INSTRUCTION = 1;
const u32 RM_UNTIL_HALT = 2;
const u32 RM_UNTIL_END = 3;

// The end of the code injected for test pusposes
global u16 testCodeEndsAt;

// The length of test input sequence
global u32 testInputLength;

// The index of the next test input
global u32 nextTestInput;

// The length of the memory access log
global u32 memLogLength;

// The length of the I/O access log
global u32 ioLogLength;

// ============================================================================
// Test machine specific functions

// Prepares the machine for running a test
export void prepareTest(u8 mode, u16 codeEnds) {
  testRunMode = mode;
  testCodeEndsAt = codeEnds;
  nextTestInput = 0;
  memLogLength = 0;
  ioLogLength = 0;
}

// Sets the length of the test input sequence
export void setTestInputLength(u32 length) {
  testInputLength = length;
}

// Returns the length of the collected memory log
export u32 getMemLogLength() {
  return memLogLength;
}

// Returns the length of the collected I/O log
export u32 getIoLogLength() {
  return ioLogLength;
}

// Runs the preapred test code
export void runTestCode() {
  do {
    executeCpuCycle();
    if (testRunMode <= RM_ONE_INSTRUCTION) {
      return;
    }

    if (testRunMode == RM_UNTIL_HALT) {
      if (cpuSignalFlags & SIG_HLT) {
        return;
      }
    }

    if (pc >= testCodeEndsAt) {
      return;
    }
  } while (true);
}

// ============================================================================
// Generic machine functions

// Turns on the virtual machine
export void turnOnMachine() {
  setupMachine();
}

// Resets the virtual machine
export void resetMachine() {
  setupMachine();
}

// Sets up the machine
void setupMachine() {
  local i32 i = 0;
  while (i < 8) {
    blockLookupTable[i].readIndex = i * 0x2000;
    blockLookupTable[i].writeIndex = i * 0x2000;
    blockLookupTable[i].flags = RAM_BLOCK;
    i += 1;
  }
}
